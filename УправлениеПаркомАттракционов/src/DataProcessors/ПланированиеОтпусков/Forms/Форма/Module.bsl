
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьНаСервере()
	
	Если Не проверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПлановыеОтсутствия.Сотрудник,
	|	Представление(ПлановыеОтсутствия.Сотрудник) КАК СотрудникПредставление,
	|	ПлановыеОтсутствия.ДатаНачала,
	|	ПлановыеОтсутствия.ДатаОкончания,
	|	ПлановыеОтсутствия.Регистратор
	|ИЗ
	|	РегистрСведений.ПлановыеОтсутствия КАК ПлановыеОтсутствия
	|ГДЕ
	|	ПлановыеОтсутствия.ДатаНачала < &КонецПериода
	|	И ПлановыеОтсутствия.ДатаОкончания > &НачалоПериода";
	
	Запрос.УстановитьПараметр("НачалоПериода",Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода",Период.ДатаОкончания);
	
	ДиаграммаГанта.Очистить();
	
	Серия = ДиаграммаГанта.Серии.Добавить();
	Серия.Значение = "Отсутствия";
	Серия.Текст = "Отсутствия";
	
	ТочкиСотрудников = Новый Соответствие();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Точка = ТочкиСотрудников.Получить(Выборка.Сотрудник);
		
		Если Точка = Неопределено Тогда
			Точка = ДиаграммаГанта.Точки.Добавить();
			Точка.Значение = Выборка.Сотрудник;
			Точка.Текст = Выборка.СотрудникПредставление;
			ТочкиСотрудников.Вставить(Выборка.Сотрудник, Точка);
		КонецЕсли;
		
		Значение = Диаграммаганта.ПолучитьЗначение(Точка, Серия);
		Интервал = Значение.Добавить();
		Интервал.Начало = Выборка.ДатаНачала;
		Интервал.Конец = Выборка.ДатаОкончания;
		Интервал.Расшифровка = Выборка.Регистратор;
	КонецЦикла;	
	
	Диаграммаганта.АвтоОпределениеПолногоИнтервала = Ложь;	
	Диаграммаганта.УстановитьПолныйИнтервал(Период.ДатаНачала, Период.ДатаОкончания);
		
КонецПроцедуры

#КонецОбласти