// @strict-types

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)

	// регистр АктуальнаяШкалаБонуснойПпрограммы
	Движения.АктуальнаяШкалаБонуснойПпрограммы.Записывать = Истина;
	Движение = Движения.АктуальнаяШкалаБонуснойПпрограммы.Добавить();
	Движение.Период = Дата;
	Движение.Шкала = Ссылка;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если Диапазоны.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;	
	Если Диапазоны[0].НижняяГраница <> 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Нижняя граница первого диапазона должна быть равна 0";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = "Диапазоны[0].НижняяГраница";
		Сообщение.Сообщить();
	КонецЕсли;		
	
	ВерхняяГраницаПредыдущегоДиапазона = 0;
	
	Для Сч = 0 По Диапазоны.Количество() - 1 Цикл
		
		Диапазон = Диапазоны[Сч];
		
		Если Сч > 0 Тогда
			Если Диапазон.НижняяГраница <> ВерхняяГраницаПредыдущегоДиапазона Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Верхняя граница диапазона должна быть равна нижней границе следующего";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Поле = СтрШаблон("Диапазоны[%1].НижняяГраница", XMLСтрока(Сч));
				Сообщение.Сообщить();
			КонецЕсли;	
		КонецЕсли;
		
		Если Диапазон.НижняяГраница >= Диапазон.ВерхняяГраница
		   И Сч <> Диапазоны.Количество() - 1 Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Верхняя граница диапазона должна быть больше нижней границы";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Поле = СтрШаблон("Диапазоны[%1].ВерхняяГраница", XMLСтрока(Сч));
			Сообщение.Сообщить();			
		КонецЕсли;	
		
		ВерхняяГраницаПредыдущегоДиапазона = Диапазон.ВерхняяГраница;
			
	КонецЦикла;
		
	ПоследнийДиапазон = Диапазоны[Диапазоны.Количество()-1];
	
	Если ПоследнийДиапазон.ВерхняяГраница <> 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Верхняя граница последнего диапазона должна быть равна 0";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле =  СтрШаблон("Диапазоны[%1].ВерхняяГраница", XMLСтрока(Диапазоны.Количество()-1));
		Сообщение.Сообщить();
	КонецЕсли;		
		
КонецПроцедуры
#КонецОбласти